plugins {
    id 'org.springframework.boot' version '2.1.8.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'idea'
}

group = 'com.examples.games'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11';
targetCompatibility = '11';

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    compileOnly 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'
}

task installReact(type: Exec) {
    workingDir 'src/main/reactjs'
    commandLine 'npm'
    args = ['install']
}

task buildReact(type: Exec, dependsOn: installReact) {
    workingDir 'src/main/reactjs'
    commandLine 'npm'
    args = ['run', 'build']
}

task copyReactResources(type: Copy, dependsOn: buildReact) {
    from 'src/main/reactjs/build'
    into 'build/resources/main/public'
}

processResources.dependsOn copyReactResources